#
#
#    Nawabs -- The Anti package manager for Nim
#        (c) Copyright 2016 Andreas Rumpf
#
#    See the file "license.txt", included in this
#    distribution, for details about the copyright.


## Recipe handling. A 'recipe' is a NimScript that produces the same build.

import os, osproc, strutils

proc error*(msg: string) = quit "[Error] " & msg

proc exec*(cmd: string) =
  if execShellCmd(cmd) != 0:
    error "exernal program failed: " & cmd

proc projToKey(proj: string): string =
  result = newStringOfCap(proj.len)
  var pendingDash = false
  for c in proj:
    if c in {'A'..'Z', 'a'..'z', '0'..'9'}:
      if pendingDash:
        if result.len > 0: result.add '_'
        pendingDash = false
      result.add toLowerAscii(c)
    else:
      pendingDash = true
  result.add ".nims"

const
  recipesDirName* = "recipes_"
  utils = "recipe_utils"

template recipesDir(): untyped = recipesDirName

proc toRecipe*(package: string): string = recipesDir() / package.projToKey

proc gitExec(dir, proj: string) =
  exec("git --git-dir " & quoteShell(dir / ".git") & " " & proj)

template withDir*(dir, body) =
  let oldDir = getCurrentDir()
  try:
    setCurrentDir(dir)
    body
  finally:
    setCurrentDir(oldDir)

proc writeHelper() =
  writeFile(utils & ".nim", """
# Generated by Nawabs.

import ospaths

const paramIdx = when defined(nimvm): 3 else: 1
let pinnedBuild = paramCount() >= paramIdx and paramStr(paramIdx) == "pinned"
let updatedBuild = paramCount() >= paramIdx and paramStr(paramIdx) == "update"

template withDir*(dir, body) =
  let oldDir = getCurrentDir()
  try:
    setCurrentDir(dir)
    body
  finally:
    setCurrentDir(oldDir)

template gitDep*(name, url, commit) =
  let g = name / ".git"
  if not dirExists(g): exec "git clone " & url
  if pinnedBuild:
    exec "git --git-dir " & g & " checkout " & commit
  elif updatedBuild:
    exec "git --git-dir " & g & " pull"

template hgDep*(name, url, commit) =
  if not dirExists("$1/.hg"): exec "hg clone $2"
  if pinnedBuild:
    withDir "$1":
      exec "hg update -c $3"
  elif updatedBuild:
    withDir "$1":
      exec "hg pull"
""")

proc nailDeps(proj, val: string; deps: seq[string]): string =
  result = """
# Generated by Nawabs.

import $1

""" % utils
  for d in deps:
    if dirExists(d / ".git"):
      let url = execProcess("git --git-dir " & (d / ".git") & " remote get-url origin").strip()
      let commit = execProcess("git --git-dir " & (d / ".git") & " log -1 --pretty=format:%H").strip()

      result.addf("""gitDep("$1", "$2", "$3")""", d, url, commit)
    elif dirExists(d / ".hg"):
      let oldDir = getCurrentDir()
      try:
        setCurrentDir(d)
        let url = execProcess("hg paths " & d).strip()
        let commit = execProcess("hg id -i").strip()
        result.addf("""hgDep("$1", "$2", "$3")""", d, url, commit)
      finally:
        setCurrentDir(oldDir)
  result.add "\n\nwithDir \"" & proj & "\":\n"
  result.add "  exec \"\"\"" & val & "\"\"\"\n"

proc init*() =
  let dir = recipesDir()
  if not dirExists(dir):
    createDir dir
    withDir dir:
      exec "git init"
      writeHelper()
      exec "git add " & utils & ".nim"
      exec "git commit -am \"nawabs: first commit\""

proc writeRecipe*(proj, val: string; path: seq[string]) =
  try:
    let dir = recipesDir()
    let dest = dir / proj.projToKey
    writeFile dest, nailDeps(proj, val, path)
    gitExec dir, "add " & dest
    gitExec dir, "commit -am \"nawabs: automatic commit\""
  except IOError:
    discard "failure to write a recipe does no harm"
